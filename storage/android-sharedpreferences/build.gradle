plugins {
    id "com.android.library"
    id "maven-publish"
    id "signing"
    id "org.jetbrains.kotlin.android"
}
apply from: "$rootDir/pom.gradle"

// ==================================
// Patch
def patch = 0
// ==================================

def vName = versions.storage.pkg + patch
android {
    namespace "br.dev.dig.storage.storage.android.sp"
    compileSdk versions.android.target

    defaultConfig {
        minSdk versions.android.minimun
        targetSdk versions.android.target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }
    kotlinOptions {
        jvmTarget = versions.java
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            //noinspection GroovyAssignabilityCheck
            storageAndroidSharedPreferences(MavenPublication) {
                //noinspection GroovyAssignabilityCheck
                from components.release
                groupId "br.dev.dig.storage"
                version vName
                artifactId "storage-android-sharedpreferences"
                pom { pom ->
                    //noinspection GroovyAssignabilityCheck
                    applyPom(pom, "Simple Storage - Storage with SharedPreferences", "Use SharedPreferences as backend NoSQL database")
                }
            }
        }
        repositories {
            if (deploy.central.enabled) {
                maven {
                    name = "Central"
                    url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                    credentials {
                        username = deploy.central.username
                        password = deploy.central.password
                    }
                }
            }
        }
        if (deploy.signing.enabled) {
            signing {
                useInMemoryPgpKeys((String) deploy.signing.key_id, (String) deploy.signing.secret, (String) deploy.signing.password)
                sign publishing.publications.storageAndroidSharedPreferences
            }
        }
    }
}

dependencies {
    implementation project(":core")
}