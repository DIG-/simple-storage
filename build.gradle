// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.java_version = JavaVersion.VERSION_1_8
    ext.android_version_min = 21
    ext.android_version_target = 32
    final Properties localProp = new Properties()
    def localPropFile = (new File(rootDir, "local.properties"))
    if (localPropFile.exists()) {
        localPropFile.withInputStream { localProp.load(it) }
    }
    def findPropertyOrEnvironment = { String key ->
        findProperty(key) ?: localProp.getProperty(key) ?: System.getenv(key)
    }

    // This file must be the text version (--armor)
    String signingFilename = findPropertyOrEnvironment("GPG_SIGNING_KEY_FILE")
    File signingFile = null
    if (signingFilename != null) {
        signingFile = file(signingFilename)
        if (!signingFile.exists()) {
            signingFile = new File(rootDir, signingFilename)
            if (!signingFile.exists()) {
                signingFile = null
            }
        }
    }

    ext.isAndroidEnabled = !(new File(rootDir, "disable-android.gradle")).exists()
    ext.deploy = [
            "central": [
                    "enabled" : findPropertyOrEnvironment("MVN_CENTRAL_USERNAME") != null,
                    "username": findPropertyOrEnvironment("MVN_CENTRAL_USERNAME"),
                    "password": findPropertyOrEnvironment("MVN_CENTRAL_PASSWORD"),
            ],
            "signing": [
                    "enabled" : signingFile != null && findPropertyOrEnvironment("GPG_SIGNING_KEY_ID") != null && findPropertyOrEnvironment("GPG_SIGNING_PASSWORD") != null,
                    "secret"  : signingFile?.getText("UTF-8"),
                    "key_id"  : findPropertyOrEnvironment("GPG_SIGNING_KEY_ID"),
                    "password": findPropertyOrEnvironment("GPG_SIGNING_PASSWORD"),
            ]
    ]
}
plugins {
    id "com.android.application" version "7.1.1" apply false
    id "com.android.library" version "7.1.1" apply false
    id "org.jetbrains.kotlin.android" version "1.5.30" apply false
    id "org.jetbrains.kotlin.jvm" version "1.5.30" apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}