plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
    id "signing"
}
apply from: "$rootDir/pom.gradle"

java {
    withSourcesJar()
    sourceCompatibility = versions.java
    targetCompatibility = versions.java
}
compileKotlin {
    kotlinOptions.jvmTarget = versions.java
}

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        serializerGson(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            from components.java
            groupId "br.dev.dig.storage"
            version "1.0.0"
            artifactId "serializer-gson"
            pom { pom ->
                //noinspection GroovyAssignabilityCheck
                applyPom(pom, "Simple Storage - Serializer with Gson", "Simple library for NoSQL wrapper with cryptography support")
            }
        }
    }
    repositories {
        if (deploy.central.enabled) {
            maven {
                name = "Central"
                url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                credentials {
                    username = deploy.central.username
                    password = deploy.central.password
                }
            }
        }
    }
    if (deploy.signing.enabled) {
        signing {
            useInMemoryPgpKeys((String) deploy.signing.key_id, (String) deploy.signing.secret, (String) deploy.signing.password)
            sign publishing.publications.serializerGson
        }
    }
}

dependencies {
    implementation project(":core")
    implementation "com.google.code.gson:gson:2.8.9"
}